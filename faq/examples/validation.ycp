/**
 * Example for validation functions
 */
{
    import "Address";
    import "IP";
    import "Netmask";
    import "Popup";
    import "URL";

    term contents = `VBox (`HSpacing (50),
	`VSpacing (2),
	`TextEntry (`id (`ip), "Enter IP"),
	`PushButton (`id (`checkip), "Validate"),
	`VSpacing (2),

	`TextEntry (`id (`address), "Enter adress (IP or hostname)"),
	`PushButton (`id (`checkaddress), "Validate"),
	`VSpacing (2),

	`TextEntry (`id (`netmask), "Enter Netmask"),
	`PushButton (`id (`checknetmask), "Validate"),
	`VSpacing (2),

	`TextEntry (`id (`url), "Enter URL"),
	`HBox (
	    `PushButton (`id (`checkurl), "Validate"),
	    `PushButton (`id (`checkhttp), "HTTP only")
	),
	`VSpacing (2),

	`PushButton (`id (`exit), "Exit")
    );
    UI::OpenDialog (contents);

    any ret	= nil;
    repeat
    {
	ret		= UI::UserInput ();
	string ip	= (string) UI::QueryWidget (`id(`ip), `Value);
	string address	= (string) UI::QueryWidget (`id(`address), `Value);
	string netmask	= (string) UI::QueryWidget (`id(`netmask), `Value);
	string url	= (string) UI::QueryWidget (`id(`url), `Value);

	if (ret == `checkip)
	{
	    if (IP::Check4 (ip))
		Popup::Message ("OK");
	    else
		Popup::Error (IP::Valid4 ());
	}
	if (ret == `checkaddress)
	{
	    if (Address::Check4 (address))
		Popup::Message ("OK");
	    else
		Popup::Error (Address::Valid4 ());
	}
	if (ret == `checknetmask)
	{
	    if (Netmask::Check4 (netmask))
		Popup::Message ("OK");
	    else
		Popup::Error ("Not OK");
	}
	if (ret == `checkurl)
	{
	    if (URL::Check (url))
		Popup::Message ("OK");
	    else
		Popup::Error ("not ok");
	    y2internal ("parsed: %1", URL::Parse (url));
	}
	// do some own checking for url
	if (ret == `checkhttp)
	{
	    if (URL::Check (url))
	    {
		map parsed = URL::Parse (url);
		y2milestone ("parsed: %1", parsed);
		if (parsed["scheme"]:"" != "http")
		{
		    Popup::Error ("not http");
		}
		else
		{
		    Popup::Message ("OK");
		}
	    }
	    else
	    {
		Popup::Error ("not ok");
	    }
	}
    } until (ret == `exit || ret == `cancel);

    UI::CloseDialog ();

/* some examples of URL checks:
y2milestone("%1", URL::Check("http://a:b@www.suse.cz:33/ahoj/nekde?neco#blah"));
y2milestone("%1", URL::Check("ftp://www.suse.cz/ah"));
y2milestone("%1", URL::Check("ftp://www.suse.cz:22/ah"));
y2milestone("%1", URL::Check("www.suse.cz ah"));
*/

}
