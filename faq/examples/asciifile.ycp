/**
 * Example for text file modification
 */
{
    import "AsciiFile";

    // let's have some file at the /tmp/simple-file location
    SCR::Write (.target.string, "/tmp/simple-file", "first line\nsecond line");

    // now read it into the map using AsciiFile
    map file	= $[];
    AsciiFile::ReadFile (file, "/tmp/simple-file");

    // modify line by line and save the modified file
    integer i	= 1;
    foreach (integer number, map linemap, file["l"]:$[], {
	y2internal ("line: %1", linemap);
	string line	= linemap["line"]:"";
	file["l",number,"line"]	= sformat ("%1.%2", i, line);
	i = i + 1;
    });
    AsciiFile::RewriteFile (file, "/tmp/simple-file");

    // ----------------------------------------
    // now, do the same thing using system agent...

    SCR::Write (.target.string, "/tmp/simple-file", "first line\nsecond line");
    i = 1;
    string contents = (string) SCR::Read (.target.string, "/tmp/simple-file");
    string new_contents = "";
    foreach (string line, splitstring (contents, "\n"), {
	y2internal ("line: %1", line);
	new_contents	= new_contents + sformat ("%1.%2\n", i, line);
	i = i + 1;
    });
    SCR::Write (.target.string, "/tmp/simple-file", new_contents);
}
